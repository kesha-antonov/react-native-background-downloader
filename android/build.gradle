apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

// Add this function in case you don't have it already
def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

android {
    def agpVersion = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION
    // Check AGP version for backward compatibility reasons (for RN < 0.71)
    if (agpVersion.tokenize('.')[0].toInteger() >= 7) {
        namespace "com.eko"
    }

    compileSdkVersion safeExtGet('compileSdkVersion', 36)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 24)
        targetSdkVersion safeExtGet('targetSdkVersion', 36)
        versionCode 1
        versionName '1.0'

        buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())

        // Support for 16KB memory page sizes (Android 15+)
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    // Configure packaging options for 16KB page size support
    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }

    sourceSets {
        main {
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ['src/newarch']
            } else {
                java.srcDirs += ['src/oldarch']
            }
        }
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'

    // MMKV dependency updated for 16KB page size support
    // MMKV 2.2.0+ includes support for 16KB memory page sizes required by Android 15+
    // Uses mmkv-shared to maintain compatibility with react-native-mmkv 4+
    implementation 'com.tencent:mmkv-shared:2.2.0'

    implementation 'com.google.code.gson:gson:2.12.1'
}
